pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Io
import "../"
import "../nexus"
// WindowSwitcherService is exported by services/qmldir only. Without this the
// window-switcher target threw "WindowSwitcherService is not defined" on every
// call, so ALT+Tab did nothing from the day it landed (afcb4fd1): its suites
// stage their own IpcHandler and never reached this one.
// tests/check-singleton-imports.sh now fails a file that uses a singleton none
// of its imports export.
import "../services"

// ─────────────────────────────────────────────────────────────
// IpcManager — centralized entry point for all external IPC signals.
//
// Moving handlers here ensures that on multi-monitor setups (where 
// TopBar/PopupLayer are duplicated) only ONE handler reacts to a signal.
// ─────────────────────────────────────────────────────────────

QtObject {
    id: root

    // ── Dashboard Toggles ────────────────────────────────────

    // Which output the user is looking at, so a dashboard toggled by a keybind
    // opens on the monitor with focus rather than on all of them.
    //
    // The per-compositor answers moved into CompositorService: Hyprland has a
    // focused monitor, niri reports the output on the focused workspace, and
    // labwc reports the screens of the active toplevel. labwc got an answer out
    // of that move — it used to fall through to "the first screen", which is the
    // wrong monitor half the time on a two-monitor desk.
    function focusedScreenName() {
        const name = CompositorService.focusedOutput
        if (name !== "") return name

        // Nothing focused, or a compositor Rime has no adapter for.
        return Quickshell.screens.length > 0 ? Quickshell.screens[0].name : ""
    }

    function toggleDashboard(page) {
        if (Popups.anyOpen && !Popups.dashboardOpen) {
            Popups.closeAll()
            Popups.dashboardScreen = focusedScreenName()
            Popups.dashboardPage = page
            Popups.dashboardOpen = true
        } else if (Popups.dashboardOpen && Popups.dashboardPage !== page) {
            Popups.dashboardPage = page
        } else {
            var next = !Popups.dashboardOpen
            Popups.closeAll()
            if (next) {
                Popups.dashboardScreen = focusedScreenName()
                Popups.dashboardPage = page
            }
            Popups.dashboardOpen = next
        }
    }

    property var dashboardHome: IpcHandler {
        target: "dashboard-home"
        function toggle() { root.toggleDashboard("home") }
    }

    property var dashboardStats: IpcHandler {
        target: "dashboard-stats"
        function toggle() { root.toggleDashboard("stats") }
    }

    property var dashboardAgents: IpcHandler {
        target: "dashboard-agents"
        function toggle() { root.toggleDashboard("agents") }
    }

    property var dashboardKanban: IpcHandler {
        target: "dashboard-kanban"
        function toggle() { root.toggleDashboard("kanban") }
    }

    property var dashboardLauncher: IpcHandler {
        target: "dashboard-launcher"
        function toggle() { root.toggleDashboard("launcher") }
    }

    // A compatibility name, kept on purpose (UI/UX Phase 19). It opened the
    // Dashboard's Config tab, a second host of the whole settings app; that tab
    // is gone and settings have one home. SUPER+C and rime-os's keybind helper
    // (files/system/libexec/rime-labwc-keybinds maps this action to "settings")
    // still call this target, and user keybinds.json files name it, so it stays
    // and opens Nexus.
    property var dashboardConfig: IpcHandler {
        target: "dashboard-config"
        function toggle() {
            Popups.dashboardOpen = false
            NexusState.toggle("", root.focusedScreenName())
        }
    }

    // ── Nexus (standalone settings window) ───────────────────
    // Deliberately not routed through Popups: Nexus is a window you leave open
    // while you work, and Popups.closeAll() is wired to click-outside and to
    // compositor focus changes, which would close it constantly.
    //
    // `page` is optional on every function so a bare `nexus toggle` works as a
    // single keybind, while `nexus open keybinds` jumps straight to a page.
    property var nexus: IpcHandler {
        target: "nexus"

        function open(page: string): string {
            if (page !== "" && !PageRegistry.has(page))
                return "unknown page: " + page + " (try: " + root.nexusPageIds() + ")"
            NexusState.openAt(page, root.focusedScreenName())
            return "nexus open at " + NexusState.page
        }

        function close(): string {
            NexusState.close()
            return "nexus closed"
        }

        function toggle(page: string): string {
            if (page !== "" && !PageRegistry.has(page))
                return "unknown page: " + page + " (try: " + root.nexusPageIds() + ")"
            NexusState.toggle(page, root.focusedScreenName())
            return NexusState.open ? "nexus open at " + NexusState.page : "nexus closed"
        }

        // So `rime shell nexus --list` and tab-completion have a source of
        // truth that cannot drift from the registry.
        function pages(): string {
            return root.nexusPageIds()
        }
    }

    function nexusPageIds() {
        const ids = []
        for (const p of PageRegistry.pages)
            ids.push(p.id)
        return ids.join(" ")
    }

    // ── Display transactions ─────────────────────────────────
    // The one settings domain that can take away the pointer you would use to
    // fix it. The dialog is now built on every output so it survives the apply
    // that raised it, but "every output" is still every output the compositor
    // has left — and if that set is empty or unreadable, the only remaining way
    // to answer is from a TTY:
    //
    //     rime shell display status
    //     rime shell display revert
    //
    // Deliberately not a second implementation of the transaction: every verb
    // is the same call the dialog's buttons make.
    property var display: IpcHandler {
        target: "display"

        /// name field value — stage one change, exactly as the page does.
        function set(name: string, field: string, value: string): string {
            if (name === "" || field === "")
                return "usage: display set <output> <field> <value>"
            let v = value
            if (value === "true")  v = true
            else if (value === "false") v = false
            else if (value !== "" && !isNaN(Number(value)) && field !== "transform")
                v = Number(value)
            DisplayService.stage(name, field, v)
            return DisplayService.dirty ? "staged " + name + " " + field + "=" + value
                                        : "no output called " + name
        }

        function apply(): string {
            if (DisplayService.pending)
                return "a display change is already waiting to be confirmed"
            if (!DisplayService.dirty)
                return "nothing staged"
            DisplayService.apply()
            return "applying"
        }

        function keep(): string {
            if (!DisplayService.pending) return "nothing to keep"
            DisplayService.confirm()
            return "kept"
        }

        function revert(): string {
            DisplayService.revertApplied()
            return "reverted"
        }

        /// Everything a person on a TTY needs before deciding, in one line.
        function status(): string {
            const bits = []
            bits.push(DisplayService.pending
                ? "waiting " + DisplayService.confirmSeconds + "s"
                : "idle")
            bits.push(DisplayService.dirty ? "staged" : "clean")
            if (DisplayService.confirmScreen !== "")
                bits.push("dialog on " + DisplayService.confirmScreen)
            if (DisplayService.lastError !== "")
                bits.push("error: " + DisplayService.lastError)
            if (DisplayService.lastNotice !== "")
                bits.push("note: " + DisplayService.lastNotice)
            return bits.join(" | ")
        }

        function refresh(): string {
            DisplayService.refresh()
            return "re-reading the outputs"
        }
    }

    // ── Agents & Workspaces help (§43) ───────────────────────
    // The guide's own "Keys and commands" section prints these lines, so a user
    // who dismissed the first-run card has a documented way to get it back and
    // a keybind target for the guide itself.
    //
    // `toggle` takes nothing and `open` takes a section, because quickshell
    // requires every declared argument at the call site: `ipc call agent-help
    // open` with no argument is refused, not defaulted. One verb per arity is
    // the only shape that gives a keybind a bare command AND gives the guide a
    // way to jump to a page.
    //
    // Both pull the Agents tab up with them. A guide floating over the Home
    // page would explain a list the user cannot see.
    property var agentHelp: IpcHandler {
        target: "agent-help"

        function toggle(): string {
            if (AgentHelp.panelOpen) {
                AgentHelp.close()
                return "agent help closed"
            }
            return root.openAgentHelp(AgentHelp.section)
        }

        function open(section: string): string {
            return root.openAgentHelp(section)
        }

        function close(): string {
            AgentHelp.close()
            return "agent help closed"
        }

        // Permanent, and the one call the first-run card's button makes.
        function dismiss(): string {
            AgentHelp.dismissOnboarding()
            return "first-run card dismissed"
        }

        function reset(): string {
            AgentHelp.resetOnboarding()
            return "first-run card restored"
        }

        function state(): string {
            return (AgentHelp.panelOpen ? "guide open at " + AgentHelp.section : "guide closed")
                 + ", first-run card "
                 + (AgentHelp.showOnboarding ? "shown" : "dismissed")
        }

        function sections(): string {
            return root.agentHelpSections()
        }
    }

    function openAgentHelp(section) {
        const id = (section === undefined || section === null) ? "" : String(section)
        if (id !== "" && !agentHelpHas(id))
            return "unknown section: " + id + " (try: " + agentHelpSections() + ")"
        if (!Popups.dashboardOpen || Popups.dashboardPage !== "agents")
            toggleDashboard("agents")
        AgentHelp.open(id)
        return "agent help open at " + AgentHelp.section
    }

    // Asked of the content singleton rather than listed here, so the ids the
    // IPC accepts cannot drift from the sections the guide draws.
    function agentHelpSections() {
        const ids = []
        for (const s of AgentHelpContent.sections)
            ids.push(s.id)
        return ids.join(" ")
    }

    function agentHelpHas(id) {
        for (const s of AgentHelpContent.sections)
            if (s.id === id) return true
        return false
    }

    // ── Audio Toggles ────────────────────────────────────────

    property var audioOut: IpcHandler {
        target: "audioOut-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.audioOpen) {
                Popups.closeAll()
                Popups.audioPage = "output"
                Popups.audioOpen = true
            } else if (Popups.audioOpen && Popups.audioPage != "output") {
                Popups.audioPage = "output"
            } else {
                var next = !Popups.audioOpen
                Popups.closeAll()
                Popups.audioOpen = next
                if (next) Popups.audioPage = "output"
            }
        }
    }

    property var audioMix: IpcHandler {
        target: "audioMix-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.audioOpen) {
                Popups.closeAll()
                Popups.audioPage = "mixer"
                Popups.audioOpen = true
            } else if (Popups.audioOpen && Popups.audioPage != "mixer") {
                Popups.audioPage = "mixer"
            } else {
                var next = !Popups.audioOpen
                Popups.closeAll()
                Popups.audioOpen = next
                if (next) Popups.audioPage = "mixer"
            }
        }
    }

    property var audioIn: IpcHandler {
        target: "audioIn-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.audioOpen) {
                Popups.closeAll()
                Popups.audioPage = "input"
                Popups.audioOpen = true
            } else if (Popups.audioOpen && Popups.audioPage != "input") {
                Popups.audioPage = "input"
            } else {
                var next = !Popups.audioOpen
                Popups.closeAll()
                Popups.audioOpen = next
                if (next) Popups.audioPage = "input"
            }
        }
    }

    // ── Network Toggles ──────────────────────────────────────

    property var wifiToggle: IpcHandler {
        target: "wifi-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.networkOpen) {
                Popups.closeAll()
                Popups.networkPage = "wifi"
                Popups.networkOpen = true
            } else if (Popups.networkOpen && Popups.networkPage != "wifi") {
                Popups.networkPage = "wifi"
            } else {
                var next = !Popups.networkOpen
                Popups.closeAll()
                Popups.networkOpen = next
                if (next) Popups.networkPage = "wifi"
            }
        }
    }

    property var btToggle: IpcHandler {
        target: "bluetooth-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.networkOpen) {
                Popups.closeAll()
                Popups.networkPage = "bluetooth"
                Popups.networkOpen = true
            } else if (Popups.networkOpen && Popups.networkPage != "bluetooth") {
                Popups.networkPage = "bluetooth"
            } else {
                var next = !Popups.networkOpen
                Popups.closeAll()
                Popups.networkOpen = next
                if (next) Popups.networkPage = "bluetooth"
            }
        }
    }

    property var vpnToggle: IpcHandler {
        target: "vpn-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.networkOpen) {
                Popups.closeAll()
                Popups.networkPage = "vpn"
                Popups.networkOpen = true
            } else if (Popups.networkOpen && Popups.networkPage != "vpn") {
                Popups.networkPage = "vpn"
            } else {
                var next = !Popups.networkOpen
                Popups.closeAll()
                Popups.networkOpen = next
                if (next) Popups.networkPage = "vpn"
            }
        }
    }

    property var hotspotToggle: IpcHandler {
        target: "hotspot-toggle"
        function toggle() {
            if(Popups.anyOpen && !Popups.networkOpen) {
                Popups.closeAll()
                Popups.networkPage = "hotspot"
                Popups.networkOpen = true
            } else if (Popups.networkOpen && Popups.networkPage != "hotspot") {
                Popups.networkPage = "hotspot"
            } else {
                var next = !Popups.networkOpen
                Popups.closeAll()
                Popups.networkOpen = next
                if (next) Popups.networkPage = "hotspot"
            }
        }
    }

    // ── Misc Toggles ─────────────────────────────────────────

    // The quick controls (volume, brightness) otherwise open only on hovering
    // the right strip — no route at all from a keyboard. This is one for a
    // keybind, and the one the headless capture harness uses.
    property var quick: IpcHandler {
        target: "quick-toggle"
        function toggle() {
            var next = !Popups.quickOpen
            Popups.closeAll()
            Popups.quickOpen = next
        }
    }

    property var notification: IpcHandler {
        target: "notification-toggle"
        function toggle() {
            var next = !Popups.notificationsOpen
            Popups.closeAll()
            Popups.notificationsOpen = next
        }
    }

    // Desktop right-click menu. `open` rather than `toggle` for the mousebind
    // path: a second right-click should reposition the menu at the new cursor
    // position, not dismiss it, which is what every desktop does.
    property var contextMenu: IpcHandler {
        target: "context-menu"
        function open() {
            if (Popups.contextMenuOpen) {
                // Force the popup to re-read the pointer position.
                Popups.contextMenuOpen = false
                reopen.restart()
            } else {
                Popups.closeAll()
                Popups.contextMenuOpen = true
            }
        }
        function toggle() {
            var next = !Popups.contextMenuOpen
            Popups.closeAll()
            Popups.contextMenuOpen = next
        }
        function close() { Popups.contextMenuOpen = false }
    }

    property var reopen: Timer {
        interval: 1
        onTriggered: { Popups.closeAll(); Popups.contextMenuOpen = true }
    }

    // ── ALT+Tab window switcher ──────────────────────────────────────────────
    //
    // Four functions on one target, because they are four operations on one
    // piece of state and the shell has to see them in the order the keyboard
    // produced them.
    //
    // `commit` and `cancel` arrive from a keybind on the ALT *release*, which
    // the compositor fires every time anybody lets go of ALT — see
    // WindowSwitcherService for why the release is a compositor binding and not
    // a keyboard grab. /usr/libexec/rime-switcher filters the closed case out
    // before this is reached, so a call getting here is nearly always real; the
    // service still returns quietly when nothing is open, because "nearly
    // always" is not "always" and a stale flag file must not produce an error.
    //
    // Each returns a string so `rime shell switcher …` has something to print
    // and, more usefully, so a test can drive the switcher over IPC and read
    // back what it did.
    property var windowSwitcher: IpcHandler {
        target: "window-switcher"

        function next(): string {
            WindowSwitcherService.next()
            return root._switcherState()
        }
        function prev(): string {
            WindowSwitcherService.prev()
            return root._switcherState()
        }
        function commit(): string {
            const was = WindowSwitcherService.labelFor(WindowSwitcherService.selected)
            WindowSwitcherService.commit()
            return was === "" ? "closed" : "activated " + was
        }
        function cancel(): string {
            WindowSwitcherService.cancel()
            return "closed"
        }
    }

    function _switcherState() {
        if (!WindowSwitcherService.open) return "closed"
        return "open " + (WindowSwitcherService.index + 1)
            + "/" + WindowSwitcherService.entries.length
            + " " + WindowSwitcherService.labelFor(WindowSwitcherService.selected)
    }

    property var clipboard: IpcHandler {
        target: "clipboard-toggle"
        function toggle() {
            var next = !Popups.clipboardOpen
            Popups.closeAll()
            Popups.clipboardOpen = next
        }
    }

    property var wallpaper: IpcHandler {
        target: "wallpaper-toggle"
        function toggle() {
            var next = !Popups.wallpaperOpen
            Popups.closeAll()
            Popups.wallpaperOpen = next
        }
    }

    property var archMenu: IpcHandler {
        target: "PowerMenu-toggle"
        function toggle() {
            var next = !Popups.archMenuOpen
            Popups.closeAll()
            Popups.archMenuOpen = next
        }
    }

    property var screenRec: IpcHandler {
        target: "screenrec-on"
        function toggle() {
            if (ScreenRecService.recording) {
                 ScreenRecService.stopRecording()
             } else if (ShellState.screenRecord) {
                 ScreenRecService.cancelSetup()
             } else {
                 Popups.closeAll()
                 ShellState.screenRecord = true
             }
        }
    }

    // ── Workspace overview (SUPER+Tab) ───────────────────────────────────────
    // Rime's own where the compositor gives it what the grid needs: workspaces
    // in fixed slots, windows with geometry, and their live pictures — Hyprland.
    // niri draws an overview of its own, so the same key asks for that one; on
    // labwc there is neither and the key does nothing (rime-os's labwc keybind
    // generator does not carry this action).
    readonly property bool overviewSupported:
        CompositorService.can.workspaces && CompositorService.can.windowGeometry
        && CompositorService.can.windowPreview && CompositorService.workspaceSlots > 0
    function toggleOverview() {
        if (!root.overviewSupported) {
            CompositorService.toggleOverview()
            return
        }
        const next = !Popups.overviewOpen
        Popups.closeAll()
        if (next) Popups.overviewScreen = focusedScreenName()
        Popups.overviewOpen = next
    }
    property var overview: IpcHandler {
        target: "overview-toggle"
        function toggle() { root.toggleOverview() }
        function open(): string {
            if (!Popups.overviewOpen) root.toggleOverview()
            return Popups.overviewOpen ? "open on " + Popups.overviewScreen : "not supported here"
        }
        function close() { Popups.overviewOpen = false }
    }

    property var focusMode: IpcHandler {
        target: "focus-toggle"
        function toggle() {
            root.focusToggleRequested()
        }
    }

    // ── Push-to-talk (roadmap P1-023, ROADMAP.md §8.2) ───────────────────────
    //
    // This handler IS the "compositor-neutral global route". The three
    // compositors Rime ships have three different keybind formats and no
    // common input path — NiriBackend.qml:69 records that niri has no runtime
    // keybind capture at all, so a shell-side grab was never an option — but
    // all three can run a command, and every shell-side action already reaches
    // the running shell this way. So the neutrality is here, at the far end of
    // one `qs ipc call`, rather than in three input adapters.
    //
    // A toggle rather than hold-to-talk, and that is a compositor fact:
    // Hyprland has `bindr` and labwc has `onRelease="yes"`, niri 26.04 has no
    // release bind of any kind. Hold would have worked on two of the three the
    // acceptance criterion names. The cost of toggle is a microphone left open
    // by accident, which is why pushtotalk.js has a hard cap and why the
    // indicator names its target.
    //
    // `state()` exists for the same reason caffeine's does: so the route can be
    // driven and inspected without opening a dashboard.
    property var voicePtt: IpcHandler {
        target: "voice-ptt"
        function toggle(): string {
            PushToTalkService.toggle()
            return PushToTalkService.indicatorLabel
        }
        function state(): string {
            return PushToTalkService.phase
        }
    }

    // Exposed independently so caffeine's actual inhibitor can be tested and
    // automated without opening the dashboard.
    property var caffeine: IpcHandler {
        target: "caffeine"
        function toggle(): bool {
            ShellState.caffeine = !ShellState.caffeine
            return ShellState.caffeine
        }
        function state(): bool {
            return ShellState.caffeine
        }
    }

    signal focusToggleRequested()

    // ── The shell itself, for the OS's live update engine ────
    // `sudo rime update` can replace the running Rime Shell. Before it does,
    // it asks `qs -p /usr/share/rime-shell ipc call shell state` whether every
    // session is unlocked (a replacement under the lock screen would drop the
    // lock); after starting the new shell it asks `... shell revision` as its
    // health check. Both are reads. There is deliberately no lock, no unlock
    // and nothing else here: the engine decides, this only answers.
    property var shellInfo: IpcHandler {
        target: "shell"

        function revision(): string {
            return root.shellRevision
        }

        function state(): string {
            const lock = root.shellLockState()
            const pid = Number(Quickshell.processId)
            return JSON.stringify({
                "revision":   root.shellRevision,
                "locked":     lock.locked,
                "lockSecure": lock.lockSecure,
                "pid":        Number.isInteger(pid) && pid > 0 ? pid : null
            })
        }
    }

    // The revision THIS process loaded, read once at startup and never again.
    // The engine replaces the shell's directory before it starts the new
    // shell, so a read at call time would have the old shell report the new
    // revision and the health check would prove nothing.
    property string shellRevision: ""
    property FileView _commitFile: FileView {
        path: Quickshell.shellDir + "/.rime-shell-commit"
        blockLoading: true
        printErrors: false
    }
    Component.onCompleted: {
        const t = String(root._commitFile.text() || "").trim()
        root.shellRevision = /^[0-9A-Za-z._-]{1,64}$/.test(t) ? t : ""
    }

    // Locked unless every flag says otherwise. `capturing` is a lock asked for
    // and not yet engaged (LockState waits up to 120 ms for its picture), and
    // `unlocking` is a correct password whose release has not landed: both are
    // still locked as far as replacing the shell is concerned. Anything that
    // cannot be read counts as locked.
    function shellLockState() {
        try {
            const secure = LockState.lockSecure === true
            const locked = LockState.locked !== false || LockState.unlocking !== false
                           || LockState.capturing !== false || secure
            return { "locked": locked, "lockSecure": secure }
        } catch (e) {
            return { "locked": true, "lockSecure": false }
        }
    }

    // ── Session Lock ─────────────────────────────────────────
    // External entry point for the native lock screen (windows/Lockscreen.qml).
    // Invoked by scripts/PowerControl.sh, hypridle's lock_cmd, and
    // `loginctl lock-session` → all via:
    //   qs ipc -c "$HOME/.local/src/rime-shell" call lockscreen lock
    //
    // SECURITY: unlock() is intentionally a no-op. Unlocking over IPC would be
    // a trivial lock bypass — the ONLY path back to unlocked is a successful
    // PAM authentication inside the lock surface.
    property var lockscreen: IpcHandler {
        target: "lockscreen"

        function lock() {
            LockState.lock()
        }

        function unlock() {
            // Deliberately does nothing. See note above.
        }
    }
}
